USE rtchecks

SUBROUTINE ByteSwap(POINTER TO SINGLE xxx)
C SECTION
  register char a;
  register char * b = (char *) xxx_;
  register int  i = 0;
  register int  j = sizeof(*xxx_)-1;
  while (i<j)
    {
     a = b[i];
     b[i] = b[j];
     b[j] = a;
     i++, j--;
     }
END C SECTION
END ByteSwap

SINGLE tmpu, tmpv, tmpw, tmp
INTEGER nfmin, nfmax, nstep
FILE dnsin=OPENRO("dns.in")
REAL nu, headx, heady, UFR, VFR
INTEGER nx, ny, nz
REAL Lx, Ly, Lz
REAL cflmax, fouriermax, deltat, maxTime
INTEGER it_save, it_stat, it_max, it_cfl, it_check
INTEGER it_runtimestats, it_start, it_end, iz_stats 
INTEGER non_ph
BOOLEAN continue
READ BY NAME FROM dnsin nu, headx, heady,UFR,VFR
READ BY NAME FROM dnsin nx, ny, nz, Lx, Ly, Lz
READ BY NAME FROM dnsin cflmax, fouriermax, deltat, maxTime
READ BY NAME FROM dnsin it_save, it_stat, it_max, it_cfl, it_check 
READ BY NAME FROM dnsin it_runtimestats, it_start, it_end, iz_stats 	
READ BY NAME FROM dnsin non_ph
READ BY NAME FROM dnsin continue
CLOSE dnsin
		

VARS=STRUCTURED ARRAY(p,u,v,w) OF REAL
ARRAY(1..nx,1..ny,0..nz) OF VARS var=0

ARRAY(-2..2*nx+5) OF REAL xd=0;
ARRAY(-2..2*ny+5) OF REAL yd=0;
ARRAY(0..2*nz) OF REAL zd=0;

ASK nfmin, nfmax, nstep
LOOP FOR nop = nfmin TO nfmax BY nstep

POINTER TO STORED STRUCTURE[INTEGER nxs,nys,nzs,its
	REAL Lxs,Lys,headxs,headys,nus,times
	REAL x(-2..2*nx+5)
	REAL y(-2..2*ny+5)
	REAL z(0..2*nz)
	ARRAY(1..nx,1..ny,0..nz) OF VARS v] stored=OPENRO("Field"nop".fld")
			
        WITH stored:
        nx=nxs; ny=nys; nz=nzs; Lx=Lxs; Ly=Lys;
        xd = x;
        yd = y;
        zd = z;
        var=v;   
        xx=^xd(2*(*))
        yy=^yd(2*(*))
        zz=^zd(2*(*))
CLOSE stored

WRITE 'I am writing Field:  ' nop
  FILE vtkfile=OPEN("Field"nop".vtk")
  WRITE TO vtkfile "# vtk DataFile Version 2.0"
  WRITE TO vtkfile "channel vector field"
  WRITE TO vtkfile "BINARY"
  WRITE TO vtkfile "DATASET RECTILINEAR_GRID"
  WRITE TO vtkfile "DIMENSIONS "nx" "ny" "nz+1
  WRITE TO vtkfile "X_COORDINATES "nx" float"
  LOOP FOR ix=1 TO nx
    tmp=SINGLE(xx(ix))
    ByteSwap(^tmp); WRITE BINARY TO vtkfile tmp
  REPEAT
  WRITE TO vtkfile "Y_COORDINATES "ny" float"
  LOOP FOR iy=1 TO ny
    tmp=SINGLE(yy(iy))
    ByteSwap(^tmp); WRITE BINARY TO vtkfile tmp
  REPEAT
  WRITE TO vtkfile "Z_COORDINATES "nz+1" float"
  LOOP FOR iz=0 TO nz
    tmp=SINGLE(zz(iz))
    ByteSwap(^tmp); WRITE BINARY TO vtkfile tmp
  REPEAT
WRITE TO vtkfile "POINT_DATA " nx*ny*(nz+1)

WRITE TO vtkfile "SCALARS p float 1"
WRITE TO vtkfile "LOOKUP_TABLE default"
LOOP FOR iz=0 TO nz AND iy=1 TO ny AND ix=1 TO nx
  WITH var
      tmp=SINGLE(var(ix,iy,iz,0)); ByteSwap(^tmp); WRITE BINARY TO vtkfile tmp 
REPEAT

WRITE TO vtkfile "VECTORS velocity float"
LOOP FOR iz=0 TO nz AND iy=1 TO ny AND ix=1 TO nx
  tmpu=SINGLE(var(ix,iy,iz,1)); tmpv=SINGLE(var(ix,iy,iz,2)); tmpw=SINGLE(var(ix,iy,iz,3)) 
  ByteSwap(^tmpu); ByteSwap(^tmpv);ByteSwap(^tmpw); WRITE BINARY TO vtkfile tmpu,tmpv,tmpw 
REPEAT 

CLOSE vtkfile 
REPEAT
