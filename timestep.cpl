#define lapl(f) [f(1,0,0)+f(-1,0,0)]*d2x+[f(0,1,0)+f(0,-1,0)]*d2y+f(0,0,1)*d2zp+f(0,0,-1)*d2zm-f(0,0,0)*d2z0
#define laplp(f) [f(1,0,0)+f(-1,0,0)]*d2x+[f(0,1,0)+f(0,-1,0)]*d2y+f(0,0,1)*d2zpp+f(0,0,-1)*d2zpm-f(0,0,0)*d2zp0
ARRAY[var.LO1..var.HI1,var.LO2..var.HI2] OF POINTER TO ARRAY(*) OF REAL buimbc,bvimbc,bwimbc,tuimbc,tvimbc,twimbc
REAL d1x=1/deltax,d1y=1/deltay,d2x=1/deltax/deltax,d2y=1/deltay/deltay
STRUCTURE(REAL d2zp,d2zm,d2z0,d1z,d1zp,d2zpp,d2zpm,d2zp0) zc(1..nz-1)

DO WITH zc(iz)
  d1z=1/[zd(2*iz+1)-zd(2*iz-1)]
  d1zm=1/[zz(iz)-zz(iz-1)]
  d2zm=d1z*d1zm
  d1zp=1/[zz(iz+1)-zz(iz)]
  d2zp=d1z*d1zp
  d2z0=d2zm+d2zp+2*d2x+2*d2y
  IF iz<nz-1 THEN
    d2zpp=1/[zd(2*iz+3)-zd(2*iz+1)]*d1zp
    d2zpm=d1z*d1zp
    d2zp0=d2zpm+d2zpp+2*d2x+2*d2y
  END IF
FOR iz=1 TO nz-1

SUBROUTINE AdamsB(REAL val^,oldrsd^,rsd)
  val=val+deltat*[1.5*rsd-0.5*oldrsd]
  oldrsd=rsd
END AdamsB

SUBROUTINE rai1(REAL val^,oldrsd^,rsd)
  val=val+deltat*64/120*rsd
  oldrsd=rsd
END rai1

SUBROUTINE rai2(REAL val^,oldrsd^,rsd)
  val=val+deltat*(50/120*rsd-34/120*oldrsd)
  oldrsd=rsd
END rai2

SUBROUTINE rai3(REAL val^,oldrsd^,rsd)
  val=val+deltat*(90/120*rsd-50/120*oldrsd)
END rai3

newARRAY=ARRAY(1..nz-1,1..3) OF REAL
SUBROUTINE linestep[SUBROUTINE(REAL val^,oldrsd^,rsd) timescheme; INTEGER ix,iy; REAL dts; POINTER TO newARRAY new; TENSEGRITY this^]
  LOOP FOR iz=1 TO nz-1
    new(iz)=var(ix,iy,iz,1..3)
    WITH var(ix+*,iy+*,iz+*),zc(iz)
    IF iz>=buimbc(ix,iy).LO AND iz<=tuimbc(ix,iy).HI THEN
      REAL rsdu=lapl(u)*nu-({[u(1,0,0)+u(0,0,0)]^2-[u(-1,0,0)+u(0,0,0)]^2}*d1x+
           {[u(0,1,0)+u(0,0,0)]*[v(1,0,0)+v(0,0,0)]-[u(0,-1,0)+u(0,0,0)]*[v(1,-1,0)+v(0,-1,0)]}*d1y+
	   {[u(0,0,1)+u(0,0,0)]*[w(1,0,0)+w(0,0,0)]-[u(0,0,-1)+u(0,0,0)]*[w(1,0,-1)+w(0,0,-1)]}*d1z)/4
      timescheme(new(iz,1),old(ix,iy,iz).u,rsdu)
      new(iz,1)=~+(headx-[p(1,0,0)-p(0,0,0)]*d1x)*dts
      IF iz<=buimbc(ix,iy).HI THEN new(iz,1)=~/[1+dts*buimbc(ix,iy,iz)]
      IF iz>=tuimbc(ix,iy).LO THEN new(iz,1)=~/[1+dts*tuimbc(ix,iy,iz)]
    END IF
    IF iz>=bvimbc(ix,iy).LO AND iz<=tvimbc(ix,iy).HI THEN
      REAL rsdv=lapl(v)*nu-({[v(0,1,0)+v(0,0,0)]^2-[v(0,-1,0)+v(0,0,0)]^2}*d1y+
           {[v(1,0,0)+v(0,0,0)]*[u(0,1,0)+u(0,0,0)]-[v(-1,0,0)+v(0,0,0)]*[u(-1,1,0)+u(-1,0,0)]}*d1x+
	   {[v(0,0,1)+v(0,0,0)]*[w(0,1,0)+w(0,0,0)]-[v(0,0,-1)+v(0,0,0)]*[w(0,1,-1)+w(0,0,-1)]}*d1z)/4
      timescheme(new(iz,2),old(ix,iy,iz).v,rsdv)
      new(iz,2)=~+(heady-[p(0,1,0)-p(0,0,0)]*d1y)*dts
      IF iz<=bvimbc(ix,iy).HI THEN new(iz,2)=~/[1+dts*bvimbc(ix,iy,iz)]
      IF iz>=tvimbc(ix,iy).LO THEN new(iz,2)=~/[1+dts*tvimbc(ix,iy,iz)]
    END IF
    IF iz>=bwimbc(ix,iy).LO AND iz<=twimbc(ix,iy).HI THEN
      REAL rsdw=laplp(w)*nu-({[w(0,0,1)+w(0,0,0)]^2-[w(0,0,-1)+w(0,0,0)]^2}*d1zp+
	   {[w(1,0,0)+w(0,0,0)]*[u(0,0,1)+u(0,0,0)]-[w(-1,0,0)+w(0,0,0)]*[u(-1,0,1)+u(-1,0,0)]}*d1x+
	   {[w(0,1,0)+w(0,0,0)]*[v(0,0,1)+v(0,0,0)]-[w(0,-1,0)+w(0,0,0)]*[v(0,-1,1)+v(0,-1,0)]}*d1y)/4
      timescheme(new(iz,3),old(ix,iy,iz).w,rsdw)
      new(iz,3)=~-[p(0,0,1)-p(0,0,0)]*d1zp*dts
      IF iz<=bwimbc(ix,iy).HI THEN new(iz,3)=new(iz,3)/[1+dts*bwimbc(ix,iy,iz)]+ \
                                              dts*bwimbc(ix,iy,iz)/[1+dts*bwimbc(ix,iy,iz)] * bwbc(ix*deltax,iy*deltay,this)
      IF iz>=twimbc(ix,iy).LO THEN new(iz,3)=new(iz,3)/[1+dts*twimbc(ix,iy,iz)]+ \
                                              dts*twimbc(ix,iy,iz)/[1+dts*twimbc(ix,iy,iz)] * twbc(ix*deltax,iy*deltay)
    END IF
  REPEAT
END linestep

REAL M(1..nz-1,-1..1)
LOOP FOR iz=1 TO nz-1
  M(iz,-1)=zc(iz).d2zm
  M(iz,1)=zc(iz).d2zp
  M(iz,0)=-zc(iz).d2z0
REPEAT
M(LO,0)=~+M(LO,-1)
M(HI,0)=~+M(HI,1)
LUdecomp M
sor=1.5
SUBROUTINE pressurelinestep(INTEGER ix,iy; REAL dts)
  ARRAY(1..nz-1) OF REAL phi
  LOOP FOR iz=1 TO nz-1
    WITH var(ix+*,iy+*,iz+*),zc(iz)
    phi(iz)={[u(0,0,0)-u(-1,0,0)]*d1x+[v(0,0,0)-v(0,-1,0)]*d1y+[w(0,0,0)-w(0,0,-1)]*d1z}*sor
  REPEAT
  phi=M\~
  LOOP FOR iz=1 TO nz-1
    BOOLEAN updatep=NO
    WITH var(ix+*,iy+*,iz+*)
    IF iz>=buimbc(ix,iy).LO AND iz<=tuimbc(ix,iy).HI THEN updatep=YES; u(0,0,0)=~+phi(iz)*{IF iz<=buimbc(ix,iy).HI THEN d1x/[1+dts*buimbc(ix,iy,iz)] ELSE IF iz>=tuimbc(ix,iy).LO THEN d1x/[1+dts*tuimbc(ix,iy,iz)] ELSE d1x}
    IF iz>=buimbc(ix-1,iy).LO AND iz<=tuimbc(ix-1,iy).HI THEN updatep=YES; u(-1,0,0)=~-phi(iz)*{IF iz<=buimbc(ix-1,iy).HI THEN d1x/[1+dts*buimbc(ix-1,iy,iz)] ELSE IF iz>=tuimbc(ix-1,iy).LO THEN d1x/[1+dts*tuimbc(ix-1,iy,iz)] ELSE d1x}
    
    IF iz>=bvimbc(ix,iy).LO AND iz<=tvimbc(ix,iy).HI THEN updatep=YES; v(0,0,0)=~+phi(iz)*{IF iz<=bvimbc(ix,iy).HI THEN d1y/[1+dts*bvimbc(ix,iy,iz)] ELSE IF iz>=tvimbc(ix,iy).LO THEN d1y/[1+dts*tvimbc(ix,iy,iz)] ELSE d1y}
    IF iz>=bvimbc(ix,iy-1).LO AND iz<=tvimbc(ix,iy-1).HI THEN updatep=YES; v(0,-1,0)=~-phi(iz)*{IF iz<=bvimbc(ix,iy-1).HI THEN d1y/[1+dts*bvimbc(ix,iy-1,iz)] ELSE IF iz>=tvimbc(ix,iy-1).LO THEN d1y/[1+dts*tvimbc(ix,iy-1,iz)] ELSE d1y}

    IF iz>=bwimbc(ix,iy).LO AND iz<=twimbc(ix,iy).HI THEN updatep=YES; w(0,0,0)=~+phi(iz)*{IF iz<=bwimbc(ix,iy).HI THEN zc(iz).d1zp/[1+dts*bwimbc(ix,iy,iz)] ELSE IF iz>=twimbc(ix,iy).LO THEN zc(iz).d1zp/[1+dts*twimbc(ix,iy,iz)] ELSE zc(iz).d1zp}
    IF iz-1>=bwimbc(ix,iy).LO AND iz-1<=twimbc(ix,iy).HI THEN updatep=YES; w(0,0,-1)=~-phi(iz)*{IF iz-1<=bwimbc(ix,iy).HI THEN zc(iz-1).d1zp/[1+dts*bwimbc(ix,iy,iz-1)] ELSE IF iz-1>=twimbc(ix,iy).LO THEN zc(iz-1).d1zp/[1+dts*twimbc(ix,iy,iz-1)] ELSE zc(iz-1).d1zp}

    IF updatep THEN p(0,0,0)=~+phi(iz)/dts ELSE p(0,0,0)=0.0
!    ! XXX
!    p(0,0,0)=~+phi(iz)/dts
  REPEAT
END pressurelinestep

SUBROUTINE courant(REAL cou^)
  cou=0
  LOOP FOR ix=var.LO1+1 TO var.HI1-1 AND iy=var.LO2+1 TO var.HI2-1 AND iz=1 TO nz-1
    IF MAX[(var(ix,iy,iz).u/deltax,var(ix,iy,iz).v/deltay,var(ix,iy,iz).w/[zd(2*iz+1)-zd(2*iz-1)])]>cou THEN cou=MAX[(var(ix,iy,iz).u/deltax,var(ix,iy,iz).v/deltay,var(ix,iy,iz).w/[zd(2*iz+1)-zd(2*iz-1)])]
  REPEAT
END courant

SUBROUTINE timestep[SUBROUTINE(REAL val^,oldrsd^,rsd) timescheme; TENSEGRITY this^]
  pbc(var(*,*,**))
  REAL dts=0, testval=1
  timescheme(dts,testval,testval)
  newARRAY new(var.LO1+1..var.HI1-1,var.LO2+1..var.HI2-1)
  LOOP FOR ix=var.LO1+2 TO var.HI1-2 AND iy=var.LO2+2 TO var.HI2-2
    find_ind_panel(this,ix*deltax,iy*deltay,ix,iy,0,0)
    linestep(timescheme,ix,iy,dts,new(ix,iy),this)
  REPEAT
  pbcwait
  LOOP FOR ix=var.LO1+1 TO var.HI1-1 AND iy=var.LO2+1 TO var.HI2-1
    EXCEPT ix>var.LO1+1 AND ix<var.HI1-1 AND iy>var.LO2+1 AND iy<var.HI2-1
    linestep(timescheme,ix,iy,dts,new(ix,iy),this)
  REPEAT
  var(LO+1..HI-1,LO+1..HI-1,1..nz-1,1..3)=new
  pbc(var(*,*,**),var(*,*,**),-1)
  pbcwait
  LOOP FOR 6 TIMES AND parity=0 TO 1
    pbcp(var.u,var.v,parity)
    LOOP FOR ix=var.LO1+2 TO var.HI1-1 AND iy=var.LO2+2+(var.LO2+ix+parity) MOD 2 TO var.HI2-1 BY 2
      pressurelinestep(ix,iy,dts)
    REPEAT
    pbcwait
    LOOP FOR ix=var.LO1+1 TO var.HI1 AND iy=var.LO2+2-(var.LO2+ix+parity) MOD 2 TO var.HI2 BY 2
      EXCEPT ix>var.LO1+1 AND ix<var.HI1 AND iy>var.LO2+1 AND iy<var.HI2
      pressurelinestep(ix,iy,dts)
    REPEAT
  REPEAT


  STRUCTURED ARRAY(x,y) OF REAL flowrate=0
  INTEGER count=0
  LOOP FOR ix=var.LO1+1 TO var.HI1-1 AND iy=var.LO2+1 TO var.HI2-1
    INC count
    flowrate.x=~+({SUM [var(ix,iy,iz).u]/zc(iz).d1z FOR iz=buimbc(ix,iy).LO TO tuimbc(ix,iy).HI}-flowrate.x)/count
    flowrate.y=~+({SUM [var(ix,iy,iz).v]/zc(iz).d1z FOR iz=bvimbc(ix,iy).LO TO tvimbc(ix,iy).HI}-flowrate.y)/count
  REPEAT
  accumulate(flowrate)
#ifndef cpg

  dum=(UFR-flowrate.x/nprocx/nprocy)/2
  dvm=(VFR-flowrate.y/nprocx/nprocy)/2

  DO var(ix,iy,iz).u=~+(IF iz>buimbc(ix,iy).HI AND iz<tuimbc(ix,iy).LO THEN dum ELSE IF iz<=buimbc(ix,iy).HI THEN dum/[1+dts*buimbc(ix,iy,iz)] ELSE dum/[1+dts*tuimbc(ix,iy,iz)]) FOR ix=var.LO1+1 TO var.HI1-1 AND iy=var.LO2+1 TO var.HI2-1 AND iz=buimbc(ix,iy).LO TO tuimbc(ix,iy).HI

  headx=~+dum/dts

  DO var(ix,iy,iz).v=~+(IF iz>bvimbc(ix,iy).HI AND iz<tvimbc(ix,iy).LO THEN dvm ELSE IF iz<=bvimbc(ix,iy).HI THEN dvm/[1+dts*bvimbc(ix,iy,iz)] ELSE dvm/[1+dts*tvimbc(ix,iy,iz)]) FOR ix=var.LO1+1 TO var.HI1-1 AND iy=var.LO2+1 TO var.HI2-1 AND iz=bvimbc(ix,iy).LO TO tvimbc(ix,iy).HI

  heady=~+dvm/dts

#endif

END timestep
